

function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}░▒▓%f%K{red}%F{251}'
    hook_com[stated]=''
  elif [[ -n $(git diff --name-only --cached 2> /dev/null) ]]; then
    hook_com[staged]='%F{2}░▒▓%f%K{2}%F{8}'
    hook_com[unstaged]=''
  else
    hook_com[stated]=''
    hook_com[unstaged]='%F{8}░▒▓%f%K{8}%F{0}'
  fi
}

function prompt_minimal_precmd {
  vcs_info
}

function prompt_minimal_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_minimal_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{green}●%f'
  zstyle ':vcs_info:*' unstagedstr '%F{yellow}●%f'
  zstyle ':vcs_info:*' formats '[%b%c%u]'
  zstyle ':vcs_info:*' actionformats " - [%b%c%u|%F{cyan}%a%f]"
  zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{cyan}%r%f'
  zstyle ':vcs_info:git*' formats '%c%u %b %f%k'
  zstyle ':vcs_info:git*' stagedstr ''
  zstyle ':vcs_info:git*' unstagedstr ''
  zstyle ':vcs_info:git*+set-message:*' hooks git_status

  esc="$(echo -e '\e')"
  up="${esc}[A"
  down="${esc}[B"

  # Define prompts.
  PROMPT="%(?.%F{8}.%F{red})%~
─── ─%f "
  # PROMPT='[%2~]${vcs_info_msg_0_}── - '
  RPROMPT='%{$up%}${vcs_info_msg_0_}%{$down%}'
}

prompt_minimal_setup "$@"

# vim: ft=zsh
